plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def getCurrentRevisionHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().substring(0, 8)
}

def propertiesFile = rootProject.file("./local.properties")
def properties = new Properties()

try {
    properties.load(new FileInputStream(propertiesFile))
} catch (Exception e) {
    println("Exception occurred while loading propertiesFile, $e")
}

def ciBuildNumber = properties['ciBuildNumber']

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        applicationId "com.jj.androidenergyconsumer"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "currentRevisionHash", "\"${getCurrentRevisionHash()}\""
        buildConfigField "int", "ciBuildNumber", ciBuildNumber ?: "-1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", "\"http://192.168.0.110:8080/\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", "\"http://192.168.0.110:8080/\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.github.black-fluffy-cat:UniversalProtocolLibrary:0.0.1'

    implementation 'io.insert-koin:koin-android:3.0.1'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    implementation "com.squareup.retrofit2:converter-jackson:2.9.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'

    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'org.mockito:mockito-inline:3.9.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation('io.insert-koin:koin-test:3.0.1') { exclude group: 'org.mockito' }
    androidTestImplementation 'io.mockk:mockk-android:1.11.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    debugImplementation 'androidx.fragment:fragment-testing:1.3.2'

    debugImplementation 'com.facebook.flipper:flipper:0.76.0'
    debugImplementation 'com.facebook.soloader:soloader:0.10.1'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.76.0'
}